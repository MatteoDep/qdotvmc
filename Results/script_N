call 'par_surf'.N
call 'par_dfp_'.N
d=0.0001
name=N
if(N=="400"){name="4 (L=0,S=0)"}else{}
if(N=="401"){name="4 (L=0,S=1)"}else{}
if(N=="420"){name="4 (L=2,S=0)"}else{}

# SURFACE
set terminal epslatex ",11" size 16cm,12cm color colortext standalone
set output 'surf'.N.'.tex'

Round(n) = gprintf("%.0e",n)
SemiAutoTicZ(ApproxTicN) = Round((GPVAL_Z_MAX - GPVAL_Z_MIN)/ApproxTicN)

set dgrid3d Npoints,Npoints
set palette rgbformulae 33,13,10
set title 'Energy vs Parameters for N='.name
set xlabel '$a$'
set ylabel '$b$'
set zlabel '$E\>[H*]$' rotate by 90
set xrange [a_min-d:a_max+d]
set yrange [b_min-d:b_max+d]
unset key
set view 45,60
splot data_surf u 1:2:3 pal w pm3d


# SURFACE a & b
set terminal epslatex ",11" size 20cm,7cm color colortext standalone
set output 'surf'.N.'ab.tex'
#set terminal epslatex ",11" size 10cm,15cm color colortext standalone
#set output 'surf'.N.'abv.tex'

set ztics SemiAutoTicZ(4)
set cbtics SemiAutoTicZ(4)
set multiplot layout 1,2 #2,1
	set title 'Energy as a function of $a$ N='.name
	set view 90,0
	#set xtics autofreq
	set xtics offset 0,-1
	unset ytics
	#set format y ''
	unset ylabel
	set xlabel offset 0,-2
	replot
	set title 'Energy as a function of $b$ N='.name
	set ylabel '$b$'
	set view 90,90
	#set ytics autofreq
	set ytics offset 0,-1
	unset xtics
	#set format x ''
	unset xlabel
	set ylabel offset 0,-2
	replot
unset multiplot


# CONTOUR
reset
set terminal epslatex ",11" size 16cm,12cm color colortext standalone
set output 'contour'.N.'.tex'

set dgrid3d Npoints,Npoints
unset surface
set contour
set cntrparam levels 100
set table "cont"
	splot data_surf u 1:2:3
unset table

reset
set table "path"
       splot data_dfp u 1:2:3
unset table
min="<(sed -n '$p' ".data_dfp.")"

reset
set palette rgbformulae 33,13,10
set title 'DFP path for N='.name
set xlabel '$a$'
set ylabel '$b$'
set zlabel '$E [H*]$' rotate by 90
set xrange [a_min+d:a_max-d]
set yrange [b_min+d:b_max-d]
set view map

splot 'cont' pal with lines notitle, \
	'path' with linespoints pointtype 7 ps 0.5 lc rgb "black" title 'DFP path', \
	min u 1:2:3 with points pt 7 ps 1.5 lc rgb "red" title 'DFP minimum'
